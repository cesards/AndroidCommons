apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven' // Jitpack
apply from: '../configuration/dependencies.gradle'

group = 'com.github.cesards'

android {
  compileSdkVersion 26
  buildToolsVersion "26.0.1"

  dataBinding.enabled = true

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 26
    versionCode 1
    versionName "1.0"
  }
}

dependencies {
  testImplementation 'junit:junit:4.12'
}

// Helps printing out compiling errors since the default num. of errors shown are 100 (Binding)
gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xmaxerrs" << "2000"
  }
}

// Build a jar with source files.
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  failOnError false
  source = android.sourceSets.main.java.sourceFiles
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  classpath += configurations.compile
}

// Build a jar with javadoc.
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}
